#!/bin/groovy

def isPr = (env.ghprbPullId && !env.ghprbPullId.empty ? true : false)
def branchName = (isPr ? ("pr" + env.ghprbPullId) : env.BRANCH_NAME)
def gitHash = null
def xiPackageUrl = null
def xmPackageUrl = null
def utils = null
def errorMessage = null
def currentStage = null

def xiPackageFilename = null
def xmPackageFilename = null

def commentOnCommit(commitHash, commentFile) {
    def markdown = readFile ("${commentFile}")
    def json = groovy.json.JsonOutput.toJson ([body: markdown])
    def jsonFile = "${commentFile}.json"
    writeFile file: "${jsonFile}", text: "${json}"
    sh ("cat ${commentFile}")
    sh ("cat ${jsonFile}")
    withCredentials([string(credentialsId: 'macios_github_comment_token', variable: 'GITHUB_COMMENT_TOKEN')]) {
        sh "curl -i -H 'Authorization: token ${GITHUB_COMMENT_TOKEN}' https://api.github.com/repos/xamarin/xamarin-macios/commits/${commitHash}/comments --data '@${jsonFile}'"
    }
    sh ("rm -f ${jsonFile}")
}

def processAtMonkeyWrench(outputFile) {
    def tmpfile = "atmonkeywrench.tmp"
    sh (script: "grep '^@MonkeyWrench: ...Summary: ' '${outputFile}' > ${tmpfile}", returnStatus: true /* don't throw exceptions if something goes wrong */)
    def lines = readFile ("${tmpfile}").split ("\n")
    echo ("length: ${lines.length}")
    for (int i = 0; i < lines.length; i++) {
        echo ("line ${i}: ${lines [i]}")
        def summary = lines [i].substring (27 /*"@MonkeyWrench: AddSummary: ".length*/).trim ()
        // remove <br/> and </a>
        summary = summary.replace ("<br/>", "").replace ("</a>", "")
        // remove <a href=''>, thus leaving only the link itself
        def href = summary.indexOf ("<a href='")
        if (href > 0) {
            def href_end = summary.indexOf ("'>", href)
            if (href_end > href)
                summary = summary.replace (summary.substring (href, href_end + 2), "")
        }
        echo (summary)
    }
}

def reportFinalStatus(err)
{
    def commentFile = "./xamarin-macios/jenkins/pr-comments.md"
    def comment = null
    if (fileExists (commentFile))
        comment = readFile ("${commentFile}")

    var status = currentBuild.currentResult
    if ("${status}" == "SUCCESS") {
        comment += "\nâœ… Jenkins job succeeded\n"
    } else {
        comment += "\nðŸ”¥ Jenkins job failed (${status}) in stage '${currentStage}' ðŸ”¥"
        if (err != "")
            comment += " : ${err}"
        comment += "\n"
    }
    writeFile (file: "${commentFile}", text: "${comment}")
    commentOnCommit ("${gitHash}", "${commentFile}")
}

timestamps {
    node('xamarin-macios && macos-high-sierra') {
        timeout (time: 9, unit: 'HOURS') {
            try {
                withEnv(["PATH=/Library/Frameworks/Mono.framework/Versions/Current/Commands:${env.PATH}"]) {
                    dir('xamarin-macios') {
                        stage('Checkout') {
                            currentStage = "${STAGE_NAME}"
                            echo "Building on ${env.NODE_NAME}"
                            scmVars = checkout scm
                            gitHash = (isPr ? (env.ghprbActualCommit) : scmVars.GIT_COMMIT)
                        }

                        stage('Provisioning') {
                            currentStage = "${STAGE_NAME}"
                            echo "Building on ${env.NODE_NAME}"
                            sh('./jenkins/provision-deps.sh')
                        }

                        stage('Build') {
                            currentStage = "${STAGE_NAME}"
                            echo "Building on ${env.NODE_NAME}"
                            sh('./jenkins/build.sh --configure-flags --enable-xamarin')
                        }

                        stage('Packaging') {
                            currentStage = "${STAGE_NAME}"
                            sh('./jenkins/build-package.sh')
                            sh('ls -la ../package')
                        }
                    }

                    stage('Signing') {
                        currentStage = "${STAGE_NAME}"
                        def xiPackages = findFiles (glob: "package/xamarin.ios-*.pkg");
                        if (xiPackages.length > 0) {
                            xiPackageFilename = xiPackages [0].name
                            echo "Created Xamarin.iOS package: ${xiPackageFilename}"
                        }
                        def xmPackages = findFiles (glob: "package/xamarin.mac-*.pkg");
                        if (xmPackages.length > 0) {
                            xmPackageFilename = xmPackages [0].name
                            echo "Created Xamarin.Mac package: ${xmPackageFilename}"
                        }
                        withCredentials([string(credentialsId: 'codesign_keychain_pw', variable: 'PRODUCTSIGN_KEYCHAIN_PASSWORD')]) {
                            sh "./xamarin-macios/jenkins/productsign.sh"
                        }
                    }

                    stage('Upload to Azure') {
                        currentStage = "${STAGE_NAME}"
                        step([
                            $class: 'WAStoragePublisher',
                            allowAnonymousAccess: true,
                            cleanUpContainer: false,
                            cntPubAccess: true,
                            containerName: "wrench",
                            doNotFailIfArchivingReturnsNothing: false,
                            doNotUploadIndividualFiles: false,
                            doNotWaitForPreviousBuild: true,
                            excludeFilesPath: '',
                            filesPath: "package/*.pkg",
                            storageAccName: 'bosstoragemirror',
                            storageCredentialId: 'bc6a99d18d7d9ca3f6bf6b19e364d564',
                            uploadArtifactsOnlyIfSuccessful: false,
                            uploadZips: false,
                            virtualPath: "jenkins/${branchName}/${gitHash}/${env.BUILD_NUMBER}/"
                        ])
                    }

                    stage('Publish builds to GitHub') {
                        currentStage = "${STAGE_NAME}"
                        utils = load "./xamarin-macios/jenkins/utils.groovy"
                        if (xiPackageFilename != null) {
                            xiPackageUrl = "https://bosstoragemirror.blob.core.windows.net/wrench/jenkins/${branchName}/${gitHash}/${env.BUILD_NUMBER}/package/${xiPackageFilename}"
                            utils.reportGitHubStatus (gitHash, 'jenkins-PKG-Xamarin.iOS', "${xiPackageUrl}", 'SUCCESS', "${xiPackageFilename}")
                        }
                        if (xmPackageFilename != null) {
                            xmPackageUrl = "https://bosstoragemirror.blob.core.windows.net/wrench/jenkins/${branchName}/${gitHash}/${env.BUILD_NUMBER}/package/${xmPackageFilename}"
                            utils.reportGitHubStatus (gitHash, 'jenkins-PKG-Xamarin.Mac', "${xmPackageUrl}", 'SUCCESS', "${xmPackageFilename}")
                        }
                    }

                    dir('xamarin-macios') {
                        stage('Launch external tests') {
                            currentStage = "${STAGE_NAME}"
                            if (isPr) {
                                echo "Currently not launching external tests for pull requests"
                            } else {
                                def outputFile = "wrench-launch-external.output.tmp"
                                try {
                                    withCredentials([string(credentialsId: 'macios_provisionator_pat', variable: 'PROVISIONATOR_VSTS_PAT')]) {
                                        sh("make -C tests wrench-launch-external MAC_PACKAGE_URL=${xmPackageUrl} IOS_PACKAGE_URL=${xiPackageUrl} WRENCH_URL=${env.RUN_DISPLAY_URL} BUILD_REVISION=${gitHash} BUILD_LANE=jenkins/${branchName} BUILD_WORK_HOST=${env.NODE_NAME} 2>&1 | tee ${outputFile}")
                                    }
                                    processAtMonkeyWrench (outputFile)
                                } catch (error) {
                                    echo ("ðŸš« Launching external tests failed: ${error} ðŸš«")
                                }
                            }
                        }

                        stage('Install Provisioning Profiles') {
                            currentStage = "${STAGE_NAME}"
                            sh('./../maccore/tools/install-qa-provisioning-profiles.sh')
                        }

                        timeout(time: 8, unit: 'HOURS') {
                            stage('Run tests') {
                                currentStage = "Test run"
                                echo "Building on ${env.NODE_NAME}"
                                if (isPr) {
                                    echo "Not running tests here because they're run on public Jenkins."
                                } else {
                                    def htmlReportLink = sh (script: './jenkins/run-tests.sh --print-html-report-link | grep "^http"', returnStdout: true).trim ()
                                    echo("Html Report: ${htmlReportLink}")
                                    sh('./jenkins/run-tests.sh --target=wrench-jenkins --publish-html-report')
                                }
                            }
                        }
                    }
                }
                reportFinalStatus ("")
            } catch (err) {
                reportFinalStatus ("${err}")
            } finally {
                stage('Cleanup') {
                    sh (script: 'make git-clean-all -C xamarin-macios', returnStatus: true /* don't throw exceptions if something goes wrong */)
                }
            }
        } // timeout
    } // node
} // timestamps
